PROCEDURE ACTUALIZAR_DETALLE_PAGO(...)
IS
  l_error VARCHAR2(2000);
  l_sysdate_detalle DATE := SYSDATE;
BEGIN
  IF IN_PAGO_ID IS NULL AND IN_DETALLE_PAGO.DETALLE_PAGO_ID IS NOT NULL THEN
    -- Actualiza detalle pago
    UPDATE MPENGES.SPG_DETALLE_PAGO
    SET ESTADO_SAAP = IN_ESTADO_SAAP,
        ESTADO = C_ESTADO_PAGO_PAGADO,
        CONSECUTIVO_PAGO = IN_CONSECUTIVO_PAGO,
        NUMERO_ASIENTO_ID = IN_ASIENTO,
        CUENTA_POR_COBRAR_ID = IN_CXC_ID,
        USUARIO_ULTIMA_MODIFICACION = IN_USUARIO,
        FECHA_ULTIMA_MODIFICACION = l_sysdate_detalle,
        MFONDOS_SCN = MFONDOS_SCN + 1
    WHERE DETALLE_PAGO_ID = IN_DETALLE_PAGO.DETALLE_PAGO_ID;

    -- Preliquidación
    UPDATE MNOMPEN.NOMP_PRELIQUIDACION_PAGO
    SET ESTADO_PRELIQUIDACION = C_PRE_ESTADO_AUTORIZADO
    WHERE PAGO_ID = IN_DETALLE_PAGO.PAGO_ID;

    -- Consolidar actualización de pago
    UPDATE MPENGES.SPG_PAGO
    SET ESTADO_COMISION = CASE WHEN IN_PAGADOR = C_PARAMETRO_FONDO THEN 'PENDIENTE_EJECUTAR' ELSE ESTADO_COMISION END,
        ESTADO_CCM_ENVIO = 'PENDIENTE_EJECUTAR',
        ESTADO_COMPROBANTE_NOMINA = 'PENDIENTE',
        FECHA_PAGO = CASE WHEN IN_PAGADOR = C_PARAMETRO_FONDO THEN l_sysdate_detalle ELSE FECHA_PAGO END,
        USUARIO_ULTIMA_MODFICACION = IN_USUARIO,
        FECHA_ULTIMA_MODIFICACION = l_sysdate_detalle,
        MFONDOS_SCN = MFONDOS_SCN + 1
    WHERE PAGO_ID = IN_DETALLE_PAGO.PAGO_ID;

  ELSE
    -- Actualiza en lote los detalles de pago a estado ERROR
    UPDATE MPENGES.SPG_DETALLE_PAGO
    SET ESTADO = C_ESTADO_ERROR,
        USUARIO_ULTIMA_MODIFICACION = IN_USUARIO,
        FECHA_ULTIMA_MODIFICACION = l_sysdate_detalle,
        MFONDOS_SCN = MFONDOS_SCN + 1
    WHERE PAGO_ID = IN_PAGO_ID
      AND OPERACION_CONCEPTO_ID IN (
        SELECT OPERACION_CONCEPTO_ID
        FROM MPENGES.SPG_OPERACION_CONCEPTO
        WHERE TIPO_PAGO_ID = C_COD_OPER_RET_PROGRAMADO
      );

    -- Actualiza preliquidación a estado error
    UPDATE MNOMPEN.NOMP_PRELIQUIDACION_PAGO
    SET ESTADO_PRELIQUIDACION = C_PRE_ESTADO_ERROR_LIQ,
        USUARIO_ULTIMA_MODIFICACION = USER,
        FECHA_ULTIMA_MODIFICACION = l_sysdate_detalle
    WHERE PAGO_ID = IN_PAGO_ID;
  END IF;

EXCEPTION
  WHEN OTHERS THEN
    l_error :=  'PAGO_ID [' ||IN_DETALLE_PAGO.PAGO_ID||'], ' || 
                'DETALLE_PAGO_ID [' ||IN_DETALLE_PAGO.DETALLE_PAGO_ID||'], ' ||
                '[ERROR_ACTUALIZANDO_DETALLE_PAGO]  - Error[' || SQLERRM || 
                '] Traza[' || dbms_utility.format_error_backtrace || ']' ;
    RAISE_APPLICATION_ERROR (-20001, l_error);
END;
